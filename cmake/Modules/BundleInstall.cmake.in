include(BundleUtilities)


# define excludes for system libraries we don't care about for distribution
list(APPEND pre_exclude_regexes "api-ms-.*") # windows API
list(APPEND pre_exclude_regexes "ext-ms-.*") # windows API
LIST(APPEND pre_exclude_regexes "ieshims.dll") # windows API
LIST(APPEND pre_exclude_regexes "emclient.dll") # windows API
LIST(APPEND pre_exclude_regexes "devicelockhelpers.dll") # windows API
LIST(APPEND pre_exclude_regexes "wpaxholder.dll") # windows API
list(APPEND pre_exclude_regexes "azure.*") # Azure
list(APPEND pre_exclude_regexes "vcruntime.*")
list(APPEND pre_exclude_regexes "msvc.*")

LIST(APPEND post_exclude_regexes ".*WINDOWS[\\/]system32.*") # windows system dlls

set(extra_directories @EXT_DEP_DIRS@)

foreach(tgt @TARGET_FILES@)
   get_filename_component(LOC ${tgt} DIRECTORY)
   list(APPEND extra_directories ${LOC})

   if(WIN32 AND ${LOC} MATCHES ".*lib$")
      # we're resolving runtime deps, so look in the runtime dirs of linked
      # lib dirs
      get_filename_component(LOC_PAR ${LOC} DIRECTORY)
      set(LOC_BIN ${LOC_PAR}/bin)
      list(APPEND extra_directories ${LOC_BIN})
   endif()
endforeach()

file(GET_RUNTIME_DEPENDENCIES
      RESOLVED_DEPENDENCIES_VAR @GR_BUNDLE_TARGET@_DEPS
      UNRESOLVED_DEPENDENCIES_VAR STUB
      DIRECTORIES ${extra_directories}
      PRE_EXCLUDE_REGEXES ${pre_exclude_regexes}
      POST_EXCLUDE_REGEXES ${post_exclude_regexes}
      EXECUTABLES @GR_BUNDLE_EXES@
      LIBRARIES @GR_BUNDLE_LIBS@
)

file(COPY ${@GR_BUNDLE_TARGET@_DEPS} DESTINATION ${CMAKE_INSTALL_PREFIX}/@GR_BUNDLE_OUT@/@GR_BUNDLE_TARGET@)

# Configure binaries for redistro and third parties for installation
# Set dirs to a list of directories where prerequisite libraries
# may be found:
set(bundle_dirs
   "${CMAKE_INSTALL_PREFIX}/bin"
   "${CMAKE_INSTALL_PREFIX}/lib"
)
if(@target_type@ STREQUAL "EXECUTABLE")
   fixup_bundle(@GR_BUNDLE_TARGET@ "${bundle_dirs}")
endif()
